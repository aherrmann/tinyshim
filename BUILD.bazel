load("@aspect_bazel_lib//lib:expand_template.bzl", "expand_template")
load("@bazel_skylib//rules:diff_test.bzl", "diff_test")
load("@bazel_skylib//rules:run_binary.bzl", "run_binary")
load("@rules_zig//zig:defs.bzl", "zig_binary", "zig_configure_binary", "zig_package", "zig_test")

zig_test(
    name = "allocator_test",
    main = "allocator.zig",
)

zig_binary(
    name = "_shim_unconfigured",
    srcs = [
        "allocator.zig",
        "payload.zig",
    ],
    csrcs = ["payload.c"],
    linker_script = "shim.ld",
    main = "shim.zig",
    tags = ["manual"],
)

zig_configure_binary(
    name = "shim_unstripped",
    actual = ":_shim_unconfigured",
    mode = "release_small",
    threaded = "single",
)

zig_test(
    name = "shim_unstripped_output_test",
    main = "shim_unstripped_output_test.zig",
    tags = ["manual"],
)

# TODO[AH] Remove this once `zig_test` supports the `data` attribute.
sh_test(
    name = "shim_unstripped_output_test_run",
    srcs = [":shim_unstripped_output_test"],
    args = ["$(rootpath :shim_unstripped)"],
    data = [":shim_unstripped"],
)

zig_test(
    name = "elf_util_test",
    main = "elf_util.zig",
)

zig_test(
    name = "shim_layout_test",
    srcs = ["elf_util.zig"],
    main = "shim_layout_test.zig",
    tags = ["manual"],
)

# TODO[AH] Remove this once `zig_test` supports the `data` attribute.
sh_test(
    name = "shim_layout_test_run",
    srcs = [":shim_layout_test"],
    args = ["$(rootpath :shim_unstripped)"],
    data = [":shim_unstripped"],
)

zig_binary(
    name = "shrink",
    srcs = ["elf_util.zig"],
    main = "shrink.zig",
    deps = ["@clap"],
)

run_binary(
    name = "shrink_shim",
    srcs = [":shim_unstripped"],
    outs = ["shim_template"],
    args = [
        "$(location :shim_unstripped)",
        "$(location :shim_template)",
        ".payload",
    ],
    tool = ":shrink",
)

expand_template(
    name = "shim_templates_zig",
    out = "shim_templates.zig",
    data = [":shim_template"],
    substitutions = {
        "{shim_template_path}": "shim_template",
    },
    template = "shim_templates.zig.tpl",
)

zig_package(
    name = "shim_templates",
    extra_srcs = [":shim_template"],
    main = ":shim_templates.zig",
)

zig_test(
    name = "mkshim_test",
    srcs = [
        "allocator.zig",
        "payload.zig",
    ],
    main = "mkshim.zig",
    deps = [
        ":shim_templates",
        "@clap",
    ],
)

zig_binary(
    name = "mkshim",
    srcs = [
        "allocator.zig",
        "payload.zig",
    ],
    main = "mkshim.zig",
    deps = [
        ":shim_templates",
        "@clap",
    ],
)

# Note, this test transitions :mkshim into the exec configuration,
# triggering an additional build of the shim binary.
# TODO[AH] Consider a zig_test instead to avoid this.
run_binary(
    name = "run_mkshim",
    outs = ["shim_generated"],
    args = [
        "/bin/echo",
        "--prepend",
        "Hello",
        "$(location :shim_generated)",
    ],
    tags = ["manual"],
    tool = ":mkshim",
)

genrule(
    name = "shim_generated_output",
    outs = ["shim_generated_output.actual"],
    cmd = "$(execpath :shim_generated) World! > $(OUTS)",
    tools = [":shim_generated"],
)

diff_test(
    name = "shim_generated_output_test",
    file1 = ":shim_output.expected",
    file2 = ":shim_generated_output.actual",
)
