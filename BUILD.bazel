load("@bazel_skylib//rules:diff_test.bzl", "diff_test")
load("@bazel_skylib//rules:run_binary.bzl", "run_binary")
load("@rules_zig//zig:defs.bzl", "zig_binary", "zig_configure_binary", "zig_test")

zig_binary(
    name = "shrink",
    main = "shrink.zig",
    deps = ["@clap"],
)

zig_test(
    name = "shrink_test",
    main = "shrink.zig",
    deps = ["@clap"],
)

zig_binary(
    name = "_hello",
    main = "hello.zig",
    tags = ["manual"],
)

zig_configure_binary(
    name = "_hello_configured",
    actual = ":_hello",
    mode = "release_small",
    threaded = "single",
)

run_binary(
    name = "_shrink_hello",
    srcs = [":_hello_configured"],
    outs = ["hello"],
    args = [
        "$(location :_hello_configured)",
        "$(location :hello)",
        ".bss",
    ],
    tool = ":shrink",
)

genrule(
    name = "hello_output",
    outs = ["hello.output"],
    cmd = "$(execpath :hello) > $(OUTS)",
    tools = [":hello"],
)

diff_test(
    name = "hello_test",
    file1 = ":hello.expected",
    file2 = ":hello.output",
)
